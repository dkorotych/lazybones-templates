import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://dl.bintray.com/pledbrook/plugins' }
    }

    dependencies {
        classpath 'uk.co.cacoethes:lazybones-gradle:1.2.4'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id "com.github.dkorotych.gradle-maven-exec" version "1.2.1"
}

apply plugin: 'uk.co.cacoethes.lazybones-templates'
apply from: "$rootDir/gradle/lazybones/lazybones.gradle"
apply from: "$rootDir/gradle/functional-test.gradle"
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

afterReleaseBuild.dependsOn publishAllTemplates

release {
    tagTemplate = 'v${version}'
    newVersionCommitMessage = '[Gradle Release Plugin][skip ci] - new version commit: '
    buildTasks = ['check', 'packageAllTemplates', 'testTemplateMavenQuickstart']
}

lazybones {
    repositoryName = "dkorotych/lazybones-templates"
    licenses = ["Apache-2.0"]
    vcsUrl = "https://github.com/dkorotych/lazybones-templates"
    if (project.hasProperty("bintrayUsername") && project.hasProperty("bintrayApiKey")) {
        repositoryUsername = project.bintrayUsername
        repositoryApiKey = project.bintrayApiKey
    }

    template "maven-quickstart" includes "logback-support", "vertx-support", "travis"
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.9"
    compile "commons-io:commons-io:2.5"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.dkorotych.lazybones-templates'
            artifactId 'maven-utils'
            version project.version - '-SNAPSHOT'
            from components.java
        }
    }
    repositories {
        maven {
            url "file://$projectDir/templates/maven-quickstart/.lazybones/repo"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

functionalTest.dependsOn installAllTemplates
packageTemplateMavenQuickstart.dependsOn publish

[packageTemplateSubtmplLogbackSupport, packageTemplateSubtmplVertxSupport, packageTemplateSubtmplTravis].each { templateTask ->
    templateTask.with {
        Publication publication = getUtilsPublication()
        filter ReplaceTokens, tokens: [
                "groupId"   : publication['groupId'],
                "artifactId": publication['artifactId'],
                "version"   : publication['version']
        ]
    }
}

ext {
    lastMavenQuickstartTemplateVersion = file('templates/maven-quickstart/VERSION').text.trim()
    javaVersion = System.getProperty('java.version').replaceFirst(/^1\.(\d+).+$/, '$1')
}

def testTemplateMavenQuickstartByLazybonesPrefix = 'testTemplateMavenQuickstartByLazybones'
tasks.addRule("Pattern: ${testTemplateMavenQuickstartByLazybonesPrefix}<version> " +
        "(where version is ${supportedLazybonesVersions}) - Create application by 'MavenQuickstart' template" +
        " and validate it") {
    String taskName ->
        if (taskName.startsWith(testTemplateMavenQuickstartByLazybonesPrefix)) {
            def lazybonesVersion = taskName - testTemplateMavenQuickstartByLazybonesPrefix

            task("createMavenApplicationByLazybones${lazybonesVersion}",
                    dependsOn: ['installTemplateMavenQuickstart']) {
                group = 'verification'
                description = "Create application by 'MavenQuickstart' template by Lazybones ${lazybonesVersion}"
                doLast {
                    def task = tasks[taskName]
                    task.workingDir = createMavenApplication(javaVersion, lazybonesVersion, lastMavenQuickstartTemplateVersion)
                    task.options {
                        threads = '1C'
                        batchMode = true
                    }
                    task.goals = [
                            'package',
                            'site'
                    ]
                }
            }

            task(taskName, type: MavenExec, dependsOn: "createMavenApplicationByLazybones${lazybonesVersion}") {
                group = 'verification'
                description = "Validate application, created by 'MavenQuickstart' template by Lazybones" +
                        " ${lazybonesVersion}"
            }
        }
}

task testTemplateMavenQuickstart {
    group = 'verification'
    description = 'Create application by "MavenQuickstart" template by all supported Lazybones versions and validate it'
}
supportedLazybonesVersions.each { version ->
    testTemplateMavenQuickstart.dependsOn << "${testTemplateMavenQuickstartByLazybonesPrefix}${version}"
}

File createMavenApplication(String javaVersion, String lazybonesVersion, String templateVersion,
                            Map<String, String> properties = [:]) {
    List<String> commands = []
    def localTemplate = file("${System.getProperty("user.home")}/.lazybones/templates/maven-quickstart-"
            + "${templateVersion}.zip")
    if (localTemplate.exists()) {
        commands = ['create', 'maven-quickstart', "${templateVersion}".toString(), '.']
    } else {
        commands = ['create', "https://dl.bintray.com/${lazybones.repositoryName}/maven-quickstart-template-"
                + "${templateVersion}.zip".toString(), '.']
    }
    def key = 'source'
    if (!properties.containsKey(key)) {
        properties.put(key, "1.${javaVersion}")
    }
    properties?.each {
        commands << "-P${it.key}=${it.value}".toString()
    }
    return executeLazybones(javaVersion, lazybonesVersion, commands)
}

task cleanUtilsRepository(type: Delete) {
    group = 'build'
    delete "${getPublishingExtension().repositories.getByName('maven').url}"
    delete "${System.getProperty('user.home')}/.groovy/grapes/${getUtilsPublication()['groupId']}"
}

clean.dependsOn cleanUtilsRepository

PublishingExtension getPublishingExtension() {
    return project.extensions.getByType(PublishingExtension)
}

Publication getUtilsPublication() {
    return getPublishingExtension().publications.getByName('maven')
}

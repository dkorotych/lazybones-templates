import java.nio.file.Files

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://dl.bintray.com/pledbrook/plugins' }
    }

    dependencies {
        classpath 'uk.co.cacoethes:lazybones-gradle:1.2.4'
    }
}

plugins {
    id 'net.researchgate.release' version '2.3.5'
    id "com.github.dkorotych.gradle-maven-exec" version "0.2"
}

apply plugin: 'uk.co.cacoethes.lazybones-templates'

afterReleaseBuild.dependsOn publishAllTemplates

release {
    tagTemplate = 'v${version}'
    buildTasks = ['check', 'packageAllTemplates']
}

lazybones {
    repositoryName = "dkorotych/lazybones-templates"
    licenses = ["Apache-2.0"]
    vcsUrl = "https://github.com/dkorotych/lazybones-templates"
    if (project.hasProperty("bintrayUsername") && project.hasProperty("bintrayApiKey")) {
        repositoryUsername = project.bintrayUsername
        repositoryApiKey = project.bintrayApiKey
    }

    template "maven-quickstart" includes "logback-support"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task testTemplateMavenQuickstart(dependsOn: 'installTemplateMavenQuickstart', type: MavenExec) {
    def directoryName = Files.createTempDirectory('maven-quickstart').toAbsolutePath().toFile().absolutePath
    def version = '1.2.1'
    exec {
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            commandLine 'cmd', '/c', 'lazybones.bat'
        } else {
            commandLine 'lazybones'
        }
        args 'create', 'maven-quickstart', "${version}", "${directoryName}"
    }
    workingDir = directoryName
    options {
        threads = '1C'
    }
    goals = [
            'clean',
            'package',
            'site'
    ]
}